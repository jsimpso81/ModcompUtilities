C-----SYMBOL.FOR
C
C     SYMBOL PRODUCES ANY SEQUENCE OF ALPHANUMERIC CHARACTERS
C     (AT ANY HEIGHT, POSITION, OR ANGLE) AND SPECIAL POINT-PLOTTING
C     SYMBOLS BY MEANS OF EITHER A "STANDARD" OR A "SPECIAL" CALL.
C     THE STANDARD CALL DRAWS TEXT, INCLUDING TITLES, CAPTIONS,
C     AND LEGENDS.  THE SPECIAL CALL DRAWS CENTERED SYMBOLS SUCH
C     AS TRIANGLES, OCTAGONS, AND BOXES FOR PLOTTING DATA POINTS
C     OR SPECIAL SYMBOLS AND TEXT.
C
C     STANDARD CALL:
C
C     CALL SYMBOL(XINCH,YINCH,HEIGHT,NASCII,ANGLE,+ICHAR)
C
C     SPECIAL CALL:
C
C     CALL SYMBOL(XINCH,YINCH,HEIGHT,NEQUIV,ANGLE,-NCODE)
C
C     PARAMETER DESCRIPTIONS:
C
C     XINCH,  FOR THE STANDARD CALL, THE X AND Y COORDINATES, IN
C     YINCH   INCHES, OF THE LOWER-LEFT CORNER OF THE FIRST CHARACTER
C             TO BE PRODUCED (BEFORE CHARACTER ROTATION).
C
C             FOR THE SPECIAL CALL, THE X AND Y COORDINATES WILL
C             REPRESENT THE GEOMETRIC CENTER OF THE CHARACTER, IF
C             NEQUIV IS 0 THROUGH 14.
C
C             FOR THE STANDARD CALL, THE PEN IS UP WHILE MOVING
C             TO THIS POINT.  FOR THE SPECIAL CALL, THE PEN
C             STATUS IS DETERMINED BY NCODE.
C
C     HEIGHT  THE HEIGHT, IN INCHES, OF THE CHARACTER.
C
C             THE CHARACTER WIDTH, INCLUDING SPACING, WILL BE EQUAL
C             TO THE HEIGHT.  A 10-CHARACTER STRING WITH EACH
C             CHARACTER .28" HIGH WOULD BE 2.8" WIDE.
C
C     NASCII  THE ALPHANUMERIC TEXT, IN INTERNAL COMPUTER
C             REPRESENTATION, FOR THE STANDARD CALL ONLY.  CHARACTERS
C             MUST BE PACKED (2 TO A WORD).
C
C             IN NASCII, THE CHARACTER INTERNAL ASCII EQUIVALENT MUST
C             BE RIGHT JUSTIFIED IF A SINGLE CHARACTER IS DESIRED AND
C             ICHAR=0.
C
C     NEQUIV  THE INTEGER EQUIVALENT OF THE DESIRED SYMBOL OR
C             ALPHANUMERIC TEXT, FOR THE SPECIAL CALL ONLY.  IF NEQUIV
C             IS 0 THROUGH 14, THE SYMBOL WILL BE CENTERED.
C
C     ANGLE   THE ANGLE AT WHICH THE ANNOTATION IS TO BE PLOTTED, IN
C             DEGREES COUNTERCLOCKWISE FROM THE X AXIS.  IF ANGLE=0,
C             THE TEXT WILL BE PLOTTED RIGHT SIDE UP, PARALLEL TO THE
C             X AXIS.
C
C     THE SIXTH AND FINAL PARAMETER HAS TWO MEANINGS:  ONE FOR THE
C     STANDARD CALL, THE OTHER FOR THE SPECIAL CALL.
C
C     +ICHAR (THE 6TH PARAMETER--FOR THE STANDARD CALL)
C
C             THE NUMBER OF CHARACTERS TO BE PLOTTED FROM NASCII, FOR
C             THE STANDARD CALL ONLY.  IF ICHAR IS NOT EQUAL TO
C             ZERO, THE DATA IS PACKED (2 TO A WORD).
C             IF ICHAR=0, ONE CHARACTER IS PRODUCED,
C             USING A SINGLE, RIGHT-JUSTIFIED NUMERIC INTEGER
C             EQUIVALENT IN THE FIRST ELEMENT OF NASCII.
C
C     -NCODE (THE 6TH PARAMETER--FOR THE SPECIAL CALL)
C
C             A NEGATIVE VALUE WHICH DETERMINES PEN UP/DOWN STATUS
C             FOR THE MOVE TO XINCH, YINCH FOR THE SPECIAL CALL ONLY.
C
C             IF NCODE:
C
C             =1
C
C             THE PEN IS UP DURING MOVEMENT, AFTER WHICH A
C             SINGLE SYMBOL IS PRODUCED.
C
C             =2 OR GREATER
C
C             THE PEN IS DOWN DURING MOVEMENT, AFTER WHICH
C             A SINGLE SYMBOL IS PRODUCED.
C
C     NOTE THAT EITHER +ICHAR OR -NCODE MUST BE USED FOR THE SIXTH
C     PARAMETER TO THE SYMBOL SUBROUTINE, NOT BOTH.
C
CCCCCCCCCCCCCCCCCCCCC  Modification History  CCCCCCCCCCCCCCCCCCCCCCCCCCCV02.01
C                                                                       V02.01
C  V02.01  03/08/88  HEB  Declare variables.                            V02.01
C                         Initialize local variables in DATA.           V02.01
C                                                                       V02.01
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCV02.01
                                                                        V02.01
      SUBROUTINE SYMBOL(XINCH,YINCH,HEIGHT,NASCII,ANGLE,ICHAR)
                                                                        V02.01
      IMPLICIT NONE                                                     V02.01
                                                                        V02.01
      INTEGER * 2   ICHAR,    ICTL,     ISYM,     NASCII                V02.01
                                                                        V02.01
      REAL * 4      ANGLE,    HEIGHT,   XINCH,    YINCH                 V02.01
                                                                        V02.01
      INTEGER*2 ZSYM(1)
C      
CJAS  DIMENSION NASCII(1)
      DIMENSION NASCII(*)
C                                                                       V02.01
      EQUIVALENCE( ISYM, ZSYM(1) )
C      
      DATA   ICTL   / 0 /,                                              V02.01
     :       ISYM   / 0 /                                               V02.01
                                                                        V02.01
C-----------------------------------------------------------------------V02.01
                                                                        V02.01
C-----IF ICHAR.LT.0, THIS IS A SPECIAL CALL
      IF (ICHAR.LT.0) GO TO 500
C-----IF ICHAR.EQ.0, DRAW ONE SYMBOL FROM THE LOW-ORDER END OF NASCII(1)
      IF (ICHAR.NE.0) GO TO 100
      ISYM=NASCII(1)
C-----CHECK RANGE
      IF (ISYM.LT.0.OR.ISYM.GT.127) GO TO 999
C-----OK TO CALL SYM1
      CALL SYM1(XINCH,YINCH,HEIGHT,ZSYM,ANGLE,1)
C-----DONE
      GO TO 999
C-----IF ICHAR.GE.1000, NOTE WILL FAIL
  100 IF (ICHAR.GE.1000) GO TO 999
C-----ORDINARY CALL, USE SYM1
      CALL SYM1(XINCH,YINCH,HEIGHT,NASCII,ANGLE,ICHAR)
C-----DONE
      GO TO 999
C-----SPECIAL CALLS COME HERE
C-----DETERMINE LINE CONTROL
  500 ICTL=-ICHAR-1
      IF (ICTL.GE.1) ICTL=9
C-----DRAW LINE TO XINCH, YINCH
      CALL DRAW(XINCH,YINCH,1,ICTL)
C-----SEPARATE CENTERED AND NON-CENTERED SYMBOLS
      ISYM=NASCII(1)-15
      IF (ISYM.LT.0) GO TO 600
C-----NON-CENTERED, TEST RANGE
      IF (ISYM.GT.95) GO TO 999
C-----CONVERT
      ISYM=ISYM+32
C-----USE EQUIVALENT SYMBOL
      CALL SYM1(XINCH,YINCH,HEIGHT,ZSYM,ANGLE,1)
C-----DONE
      GO TO 999
C-----CENTERED SYMBOLS COME HERE
  600 ISYM=ISYM+15
C-----TEST RANGE
      IF (ISYM.LT.0) GO TO 999
C-----RANGE OK, USE SYM1
      CALL SYM1(XINCH,YINCH,HEIGHT,ZSYM,ANGLE,-1)
C-----DONE
  999 RETURN
      END
