C-----SCALE.FOR
C
C     SCALE EXAMINES THE DATA VALUES IN ARRAY AND DETERMINES
C     EITHER A MINIMUM OR A MAXIMUM STARTING VALUE AND A
C     NEGATIVE OR A POSITIVE SCALING FACTOR SUCH THAT
C
C     1.      THE SCALE ANNOTATION DRAWN BY THE AXIS SUBROUTINE
C             AT EACH DIVISION PROPERLY REPRESENTS THE RANGE
C             OF REAL DATA VALUES IN THE ARRAY
C     2.      THE DATA POINTS PLOTTED BY THE LINE SUBROUTINE
C             WILL FIT A GIVEN PLOTTING AREA
C
C     THESE VALUES, CALLED FIRSTV (MINIMUM OR MAXIMUM STARTING
C     VALUE) AND DELTAV (NEGATIVE OR POSITIVE SCALING FACTOR),
C     ARE STORED AT THE END OF THE ARRAY.
C
C     FIRSTV REPRESENTS A MULTIPLE OF DELTAV WHICH IS LESS THAN
C     OR EQUAL TO THE SAMLLEST VALUE OF THE DATA IN THE ARRAY.
C     IT APPEARS AS THE FIRST ANNOTATION ON THE AXIS.  ITS UNITS
C     ARE DATA UNITS.  THE VALUE OF FIRSTV WILL BE PLOTTED AT
C     THE USER ABSOLETE PLOTTER ORIGIN.
C
C     DELTAV REPRESENTS THE NUMBER OF DATA UNITS PER INCH OF AXIS,
C     ADJUSTED SO THAT IT IS ALWAYS AN INTERVAL OF 1, 2, 4, 5,
C     OR 8 * 10**N (WHERE N IS AN EXPONENT CONSISTENT THE ORIGINAL,
C     UNADJUSTED SCALING FACTOR).  THIS SELECTION OF FACTORS
C     ALLOWS CONVENIENT INTERPOLATION OF GRAPHS.
C
C     SCALE IS USUALLY CALLED TO EXAMINE EACH ARRAY TO BE PLOTTED.
C     HOWEVER, IF THE RANGE OF DATA VALUES IS KNOWN FOR AN ARRAY,
C     THE SUBROUTINE MAY BE OMITTED, PROVIDED AN APPROPRIATE
C     FIRSTV AND DELTAV ARE SUPPLIED WHEN AXIS AND LINE ARE CALLED.
C
C     WHEN USED WITH LINE, SCALE WILL PLACE ALL DATA AND THE
C     CONNECTING LINES WITHIN THE AREA DEFINED BY TWO CALLS TO
C     SCALE (ONE CALL EACH FOR X AND Y).
C
C     PARAMETER DESCRIPTIONS:
C
C     ARRAY   THE FIRST ELEMENT OF THE ARRAY OF DATA POINTS TO BE
C             EXAMINED (THIS IS NOT NECESSARILY THE FIRST ARRAY
C             ELEMENT; IT MUST BE THE FIRST ELEMENT TO BE SCALED).
C
C     AXLEN   THE LENGTH OF THE AXIS TO WHICH THE DATA MUST
C             BE SCALED.  ITS VALUE MUST BE GREATER THAN 1 INCH.
C
C     NPTS    THE NUMBER OF DATA VALUES TO BE SCANNED FOR SCALING
C             IN THE ARRAY.  TEH DIMENSION STATEMENT FOR THE
C             ARRAY MUST SPECIFY AT LEAST TWO ELEMENTS MORE THAN
C             THE NUMBER OF VALUES SCANNED, TO ALLOW ROOM FOR
C             FIRSTV AND DELTAV AT THE UPPER LIMIT OF THE ARRAY.
C
C     INC     AN INTEGER INCREMENT WHOSE MAGNITUDE IS USED BY THE
C             SCALE SUBROUTINE TO SELECT THE DATA VALUES SCANNED
C             IN THE ARRAY.
C
C             INC IS USUALLY 1; IF INC IS 2, EVERY SECOND VALUE
C             IN THE ARRAY IS EXAMINED, ETC.
C
C             IF INC IS POSITIVE, THE SCALING FACTOR DELTAV IS
C             POSITIVE AND THE STARTING VALUE FIRSTV APPROXIMATES
C             A MINIMUM.
C
C             IF INC IS NEGATIVE, THE SCALING FACTOR DELTAV IS
C             NEGATIVE AND THE STARTING VALUE FIRSTV APPROXIMATES
C             A MAXIMUM.
C
C             IF THE MAGNITUDE OF INC IS GREATER THAN 1, THE COMPUTED
C             VALUES ARE STORED AT (INC) AND (2*INC) ELEMENTS BEYOND
C             THE LAST DATA POINT.  THE SUBSCRIPTED ELEMENT FOR
C             FIRSTV IS
C
C                     ARRAY(NPTS*INC+1)
C
C             AND FOR DELTAV IT IS
C
C                     ARRAY(NPTS*INC+INC+1)
C
      SUBROUTINE SCALE(ARRAY,AXLEN,NPTS,INC)
      DIMENSION ARRAY(*),KDELTA(10)
C     KDELTA IS USED TO CALCULATE THE SMALLEST ACCEPTABLE
C     VALUE IS DELTAV, GIVEN A PROPOSED VALUE OF DELTAV
      DATA KDELTA/1,2,4,4,5,8,8,8,10,10/
C     K IS THE DISPLACEMENT OF SUCCESSIVE VALUES IN ARRAY
      K=IABS(INC)
C     N IS THE SUBSCRIPT OF THE LAST VALUE IN ARRAY
      N=1+(NPTS-1)*K
C     DETERMINE THE MINIMUM (YO) AND THE MAXIMUM (YN) VALUES
C     IN ARRAY
      YO=ARRAY(1)
      YN=YO
      DO 1 I=1,N,K
      YS=ARRAY(I)
      IF (YS.LT.YO) YO=YS
    1 IF (YS.GT.YN) YN=YS
C     IF DESCENDING SCALE DESIRED, MAP ALL DATA, AS REPRESENTED
C     BY YO AND YN, TO THE NEGATIVES OF THE ACTUAL VALUES
      IF (INC.GE.0) GO TO 2
      YS=YO
      YO=-YN
      YN=-YS
    2 CONTINUE
C     CALCULATE INITIAL VALUE FOR DELTAV
      DELTAV=(YN-YO)/AXLEN
C     CORRECT FOR ALL DATA VALUES EQUAL
      IF (DELTAV.LE.0.0) DELTAV=ABS(YN/AXLEN)
C     CORRECT FOR ALL DATA VALUES .EQ. 0
      IF (DELTAV.EQ.0.0) DELTAV=1.0
C     SCALE DELTAV TO THE RANGE 1.LE.DELTAV.LT.10
    3 I=JFIX(ALOG10(DELTAV))
      P=10.0**I
      DELTAV=DELTAV/P
C     GET THE CEILING OF DELTAV
      I=-JFIX(-DELTAV)
C     GET THE SMALLEST ACCEPTABLE VALUE FOR DELTAV
      DELTAV=KDELTA(I)
C     RESCALE DELTAV
      DELTAV=DELTAV*P
C     CALCULATE THE BEGINNING OF THE AXIS
      AXBEG=-AFIX(YO/DELTAV)
C     CALCULATE A NEW DELTAV, TAKING AXBEG INTO ACCOUNT
      DELTAN=YN/(AXLEN-AXBEG)
C     IF DELTAV IS NOT BIG ENOUGH, TRY AGAIN
      IF (DELTAV.GE.DELTAN) GO TO 4
      DELTAV=DELTAN
      GO TO 3
    4 CONTINUE
C     IF UNSCALED ORIGIN IS AT SCALED ORIGIN, DO NOT MOVE IT
      IF (AXBEG.EQ.0.0) GO TO 5
C     CHOOSE NEW AXBEG TO CENTER DATA
      AXBEG=AXBEG+AINT(0.5*(AXLEN-AXBEG+1.0-(YN/DELTAV)))
C     IF DESCENDING SCALE DESIRED, ADJUST DELTAV
    5 IF (INC.LT.0) DELTAV=DELTAV
C     CALCULATE FIRSTV
      FIRSTV=-AXBEG*DELTAV
C     STORE FIRSTV IN ARRAY
      N=N+K
      ARRAY(N)=FIRSTV
C     STORE DELTAV IN ARRAY
      N=N+K
      ARRAY(N)=DELTAV
      RETURN
      END
