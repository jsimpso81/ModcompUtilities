C-----NOTE.FOR
      SUBROUTINE NOTE(X,Y,ITEXT,NC)
      DIMENSION X(1),Y(1)
      DIMENSION NBUF(6),ITG(2),IBUF(13)
      DIMENSION ITEXT(1)
      DIMENSION IT1(2)
      COMMON /PLTCOM/
     &SAME,PREF(2),LDC(2),C(2),RORG(2),CMAT(10,3),LMT(2),
     &XYDOTS(2),SPX,SPY,MIX,NSKP,NBAD,
     &NPLOT,MINX,MAXX,NDX,NDY,LTYPE,LWDTH,DEGRAD,
     &NBITS,NBTM1,NBYTE,NBYM1,NCHAR,NCHM1,MSK(7),IBT(16)
      EQUIVALENCE (C(1),ITG(1))
      EQUIVALENCE (T1,IT1(1))
      DATA IBUF(1)/"40/
      DATA IDASH/"55/
      DATA IDOTT/"56/,NTXT/"60/
      DATA IOV1/'##'/,IOV2/'# '/
      X0=X(1)
      Y0=Y(1)
      IF (X0.EQ.SAME) X0=PREF(1)
      IF (Y0.EQ.SAME) Y0=PREF(2)
C-----EXAMINE 'NC' FOR PROCESSING SPECIFICATIONS
      IF (NC) 200,300,50
   50 ND=NC-1000
      IF (ND.GE.0) GO TO 400
C-----**ALPHANUMERIC TEXT**
      ND=NC
  110 CALL DRAW(X0,Y0,1,0)
C-----INSERT 'ALPHA TEXT FOLLOWS' CODE INTO DATA STREAM
      I=IBT(9)+ND
      CALL POUT(1,I)
      IF (ND.NE.NC) GO TO 130
C-----INSERT ALPHA TEXT
      DO 120 I=1,ND,2
      K=LG1GRZ(ITEXT,I)
      IF (I+1.LE.ND) K=K+ISHFT(LG1GRZ(ITEXT,I+1),8)
  120 CALL POUT(1,K)
      GO TO 150
C-----COMPUTE WORD COUNT AND INSERT NUMERIC TEXT
  130 I=(ND+NBYM1)/NBYTE
      CALL POUT(I,NBUF)
C-----UPDATE PEN POSITION TO END OF TEXT LINE
  150 X0=X0+FLOAT(ND)*SPX
      Y0=Y0+FLOAT(ND)*SPY
      CALL DRAW(X0,Y0,1,0)
      RETURN
C-----**SYMBOL PLOTTING**
  200 IDS=CMAT(5,3)
      CALL DRAW(X,Y,1,IDS)
      I=MOD(IDS,10)
      IF (I.NE.0) IDS=IDS-I+9
      J=IAND(ISHFT(ITEXT(1),-8),NCHM1)
      IF (J.EQ.0) J=IAND(ITEXT(1),NCHM1)
      I=IBT(8)+J
      ND=-NC
      K=NSKP
      L=1
      DO 250 J=1,ND
      CALL DRAW(X(L),Y(L),1,IDS)
      K=K+1
      IF (K.LT.NSKP) GO TO 250
      CALL POUT(1,I)
      K=0
  250 L=L+MIX
      RETURN
C-----**INTEGER NUMBER VALUE TEXT GENERATION**
  300 T1=ITEXT(1)
      ND=0
      GO TO 500
C-----**REAL NUMBER VALUE TEXT GENERATION**
C-----(ITEXT IS ACTUALLY FL. PT. NUMBER)
  400 CONTINUE
      IT1(1)=ITEXT(1)
      IT1(2)=ITEXT(2)
C-----*NUMERIC VALUE TO ALPHANUMERIC TEXT*
C-----NRQD IS THE MINIMUM NUMBER OF CHARACTERS REQUIRED
C-----TO GUARANTEE AT LEAST ONE DIGIT TO THE LEFT OF THE REAL OR
C-----IMPLIED DECIMAL POINT.
  500 NRQD=ND+2
C-----K IS THE VALUE I WILL HAVE JUST BEFORE THE FIRST DIGIT TO THE
C-----LEFT OF THE DECIMAL WOULD BE PROCESSED IF IT WERE NOT FOR THE
C-----DECIMAL POINT ITSELF, OR ZERO IF NO DECIMAL POINT IS NEEDED.
      K=0
      IF (ND.NE.0) K=ND+2
      T2=AINT(ABS(T1)*10.0**ND+0.5)
C-----T2 IS NOW A POSITIVE, FLOATING POINT INTEGER, THE DIGITS
C-----OF WHICH REPRESENT THE ORIGINAL NUMBER.
C
C-----IF ALL DIGITS ARE ZERO, PREVENT MINUS SIGN
      IF (T2.EQ.0.0) T1=0.0
C-----ALLOW FOR TEN DIGITS.  NOTE THAT THE LOOP STARTS WITH I .EQ. 2
C-----TO ALLOW FOR THE PAD IN IBUF.
      L=11
C-----ALSO FOR A DECIMAL, IF REQUIRED.
      IF (ND.GT.0) L=L+1
      DO 520 I=2,L
      IF (I.NE.K) GO TO 510
C-----AT THIS POINT, A DECIMAL POINT IS REQUIRED.  FURTHERMORE,
C-----I-2 .EQ. K-2 .EQ. ND DIGITS TO THE RIGHT OF THE DECIMAL HAVE
C-----BEEN PROCESSED, SO THIS IS THE PLACE FOR THE DECIMAL.
      IBUF(I)=IDOTT
      GO TO 520
C-----GENERATE THE NEXT DIGIT (FROM RIGHT TO LEFT).
  510 T3=T2
      T2=AINT(T2/10.0)
      T4=T3-10.0*T2
      IF (T4.LT.0.0.OR.T4.GT.9.0) T4=0.0
      IBUF(I)=IFIX(T4)+NTXT
  520 IF (T2.EQ.0.0.AND.I.GE.NRQD) GO TO 530
C-----AT THIS POINT, TEN DIGITS HAVE BEEN PROCESSED AND
C-----MORE ARE YET REQUIRED.  PLOT ### IN PLACE OF THE NUMBER.
      ND=3
      NBUF(1)=IOV1
      NBUF(2)=IOV2
      GO TO 110
C-----THE DIGITS AND DECIMAL POINT ARE NOW IN IBUF
C-----PUT A MINUS SIGN IN IBUF IF REQUIRED.
  530 IF (T1.GE.0.0) GO TO 540
      I=I+1
      IBUF(I)=IDASH
  540 CONTINUE
C-----THE NUMBER IS NOW COMPLETELY ASSEMBLED IN IBUF IN REVERSE
C-----ORDER.  IT IS NOW REVERSED AND PACKED INTO NBUF, TWO CHARACTERS
C-----PER WORD.
      J=1
      DO 550 III=2,I,2
      II=I+2-III
      NBUF(J)=IOR(IBUF(II),ISHFT(IBUF(II-1),8))
  550 J=J+1
C-----THERE ARE NOW I-1 DESIRABLE CHARACTERS IN NBUF TO BE PLOTTED
      ND=I-1
      GO TO 110
      END
