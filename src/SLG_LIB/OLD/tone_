C-----TONE.FOR
      SUBROUTINE TONE(X,Y,NE,NA)
      DIMENSION NE (1)
      DIMENSION X(1),Y(1)
      DIMENSION YL(50),YR(50)
      LOGICAL COND1,COND2
      COMMON /IOUNIT/ LU1,LU2,LU3,LU4,LU5
      COMMON /PLTCOM/
     &SAME,PREF(2),LDC(2),C(2),RORG(2),CMAT(10,3),LMT(2),
     &XYDOTS(2),SPX,SPY,MIX,NSKP,NBAD,
     &NPLOT,MINX,MAXX,NDX,NDY,LTYPE,LWDTH,DEGRAD,
     &NBITS,NBTM1,NBYTE,NBYM1,NCHAR,NCHM1,MSK(7),IBT(16)
      DATA IDS/0/
      DATA SGNF/.0003/
      DATA MCROSS/50/
    1 FORMAT (1X,'FATAL ERROR: MORE THAN ',I3,
     &' AREA CROSSINGS IN TONE.')
      MA=IABS(NA)
      IF (NA.NE.0) GO TO 50
C-----RESET DATA DESCRIPTORS
      IDS=NE(1)
      RETURN
C-----IF NA .LT. 0, CLEAR THE ZONE, ELSE TONE IT
   50 ITONE=48
      IF (NA.LT.0) ITONE=32
C-----TONE AREA OPERATIONS
C-----DETERMINE TOTAL NUMBER OF ELEMENTS IN X,Y
      NET=0
      DO 110 I=1,MA
  110 NET=NET+NE(I)
C-----DETERMINE MAX AND MIN X COORDINATES
      XMAX=X(1)
      XLF=X(1)
      DO 150 I=1,NET
      X1=X(I)
      IF (X1.GT.XMAX) XMAX=X1
  150 IF (X1.LT.XLF) XLF=X1
C-----DETERMINE NEXT LOWEST X COORDINATE (>XLF)
  200 XRI=XMAX
      DO 240 I=1,NET
      X1=X(I)
      IF (X1.LE.XLF) GO TO 240
      IF (X1.LT.XRI) XRI=X1
  240 CONTINUE
C-----CHECK FOR LINES CROSSING CURRENT AREA
  300 N=0
      L=0
      DO 390 I=1,MA
      K=L+1
      L=K+NE(I)-1
      X1=X(L)
      Y1=Y(L)
      DO 390 J=K,L
      X2=X(J)
      Y2=Y(J)
      DLT=XLF-X1
C     THE FOLLOWING LINE IS NOT LEGAL IN MODCOMP FORTRAN, AND IS REPLACED
C     BY THE THREE STATEMENTS WHICH FOLLOW IT
C     IF (DLT.LT.0.0.EQV.X2.GT.XLF) GO TO 380
      COND1=DLT.LT.0.0
      COND2=X2.GT.XLF
      IF (COND1.AND.COND2.OR..NOT.(COND1.OR.COND2)) GO TO 380
C-----COMPUTE Y INTERSECTIONS OF LINE CROSSING CURRENT AREA
      SLOPE=(Y2-Y1)/(X2-X1)
      N=N+1
      IF (N.LE.MCROSS) GO TO 370
      WRITE (LU2,1) MCROSS
      CALL EXIT
  370 CONTINUE
      YL(N)=SLOPE*DLT+Y1
      YR(N)=SLOPE*(XRI-X1)+Y1
  380 X1=X2
  390 Y1=Y2
C-----TEST FOR SEGMENTS CROSSING CURRENT AREA
      IF (N.LE.0) GO TO 550
C-----TEST FOR SEGMENTS INTERSECTING WITHIN CURRENT AREA
      DO 440 I=1,N
      DO 440 J=I,N
      Y1=YL(I)-YL(J)
      Y2=YR(I)-YR(J)
      IF (Y1*Y2.GT.0.0.OR.ABS(Y1).LE.SGNF.OR.ABS(Y2).LE.SGNF) GO TO 440
C-----SUBSECTION CURRENT AREA
  430 XRI=XLF+(XRI-XLF)*(Y1/(Y1-Y2))
      GO TO 300
  440 CONTINUE
C-----ORDER SEGMENTS LOWEST TO HIGHEST
      L=N-1
      DO 490 I=1,L
      K=I+1
      DO 490 J=K,N
      Y1=YL(J)
      Y2=YR(J)
      IF (YL(I)-Y1.LE.SGNF.AND.YR(I)-Y2.LE.SGNF) GO TO 490
C-----SWAP SEGMENT ORDER
      YL(J)=YL(I)
      YL(I)=Y1
      YR(J)=YR(I)
      YR(I)=Y2
  490 CONTINUE
C-----OUTPUT ELEMENTARY AREAS
      DO 520 I=1,N,2
      K=I
      CALL POUT(1,ITONE)
      DO 520 J=1,2
      LDC(1)=-9999
      CALL DRAW(XLF,YL(K),1,IDS)
      LDC(1)=-9999
      CALL DRAW(XRI,YR(K),1,IDS)
  520 K=K+1
C-----UPDATE LEFT X LIMIT
  550 XLF=XRI
C-----TEST FOR AREA PROCESSING COMPLETE
      IF (XLF-XMAX) 200,999,999
  999 LDC(1)=-9999
      RETURN
      END
